import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id "fabric-loom" version "1.9-SNAPSHOT"
	id "maven-publish"
	id "org.jetbrains.kotlin.jvm" version "2.2.21"
}

version = mod_version
group = maven_group

base {
	archivesName = archives_base_name
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"hexical" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/hexical.accesswidener")
}

repositories {
	maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release/" }
	maven { url = "https://maven.kosmx.dev/" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/releases" }
    maven { url = "https://maven.ladysnake.org/releases" }
    maven { url = "https://mvn.devos.one/snapshots/" }
	maven { url = "https://maven.hexxy.media" }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"
	modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

	include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:$player_animator_version")
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:$cardinal_components_version"
	modImplementation "vazkii.patchouli:Patchouli:$minecraft_version-$patchouli_version-FABRIC"
	modImplementation "miyucomics.hexpose:hexpose:$hexpose_version"
	modCompileOnly "dev.emi:emi-fabric:$emi_version:api"
	modLocalRuntime "dev.emi:emi-fabric:$emi_version"
	modImplementation "com.samsthenerd.inline:inline-fabric:$minecraft_version-$inline_version"
	modImplementation("at.petra-k.hexcasting:hexcasting-fabric-$minecraft_version:$hexcasting_version") {
		exclude module: "phosphor"
        exclude module: "lithium"
	}

	modLocalRuntime "me.shedaniel.cloth:cloth-config-fabric:$cloth_config_version"
	modLocalRuntime "at.petra-k.paucal:paucal-fabric-$minecraft_version:$paucal_version"
	modLocalRuntime "io.github.tropheusj:serialization-hooks:0.4.99999"
}

processResources {
	inputs.property "version", version
	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

fabricApi {
	configureDataGeneration() {
		modId = "hexical"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = "17"
	}
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}
